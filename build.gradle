buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE'
        classpath 'com.github.rodm:gradle-teamcity-plugin:1.2.2'
        classpath 'net.researchgate:gradle-release:2.7.0'
    }
}

ext {
    teamcityVersion = '2017.1'
    teamcityDir = "${rootProject.projectDir}/.teamcity"
    serverOpts = "-DTC.res.disableAll=true " +
            "-Dteamcity.development.mode=true " +
            "-Dteamcity.development.shadowCopyClasses=true " +
            "-Xdebug -Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=50055"

    gradleScriptDir = "${rootProject.projectDir}/gradle"
}

group = 'io.qameta.allure'
version = version
description = 'Allure for TeamCity'

configure(rootProject) {

    apply from: "${gradleScriptDir}/release.gradle"

    task build() {
    }

    afterReleaseBuild.dependsOn(":allure-teamcity-server:publishPlugin")
}

tasks.withType(Wrapper.class) {
    gradleVersion = '6.4'
}

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
    }

    compileTestJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        options.encoding = 'UTF-8'
        options.compilerArgs += '-parameters'
    }

    dependencyManagement {
        dependencies {

            dependency 'javax.servlet:jstl:1.1.2'
            dependency 'org.zeroturnaround:zt-zip:1.12'

            dependency 'org.apache.commons:commons-compress:1.16.1'
            dependency 'org.apache.commons:commons-lang3:3.4'
            dependency 'org.apache.commons:commons-io:1.3.2'
            dependency 'org.apache.maven:maven-artifact:3.6.3'
            dependency 'commons-logging:commons-logging:1.1.3'
            dependency 'commons-httpclient:commons-httpclient:3.1'

            dependency 'com.intellij:openapi:7.0.3'
            dependency "org.jetbrains.teamcity:agent-api:$teamcityVersion"
            dependency "org.jetbrains.teamcity.internal:agent:$teamcityVersion"
            dependency "org.jetbrains.teamcity.internal:server:$teamcityVersion"

            dependency 'com.fasterxml.jackson.core:jackson-databind:2.7.2'
        }
    }

    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://download.jetbrains.com/teamcity-repository' }
    }
}

configurations.all {
    resolutionStrategy {
        force 'xml-apis:xml-apis:1.4.01'
    }
}
